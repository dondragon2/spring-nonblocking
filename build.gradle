apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'signing'

group 'io.github.dondragon2'
version '0.1'

sourceCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    compile group: 'org.springframework', name: 'spring-context', version: '5.1.7.RELEASE'
    compile group: 'org.springframework', name: 'spring-web', version: '5.1.7.RELEASE'
    compile group: 'org.springframework', name: 'spring-webmvc', version: '5.1.7.RELEASE'
    compile group: 'javax.servlet', name: 'javax.servlet-api', version: '4.0.1'
    compile group: 'org.aspectj', name: 'aspectjrt', version: '1.9.3'
    compile group: 'io.reactivex', name: 'rxjava', version: '1.3.8'
    compile group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

signing {
    sign configurations.archives
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            customizePom(pom)
            groupId project.group
            artifactId 'spring-nonblocking'
            version project.version

            from components.java

            // create the sign pom artifact
            pom.withXml {
                def pomFile = file("${project.buildDir}/generated-pom.xml")
                writeTo(pomFile)
                def pomAscFile = signing.sign(pomFile).signatureFiles[0]
                artifact(pomAscFile) {
                    classifier = null
                    extension = 'pom.asc'
                }
            }

            artifact(sourceJar) {
                classifier = 'sources'
            }
            artifact(javadocJar) {
                classifier = 'javadoc'
            }

            // create the signed artifacts
            project.tasks.signArchives.signatureFiles.each {
                artifact(it) {
                    def matcher = it.file =~ /-(sources|javadoc)\.jar\.asc$/
                    if (matcher.find()) {
                        classifier = matcher.group(1)
                    } else {
                        classifier = null
                    }
                    extension = 'jar.asc'
                }
            }
        }
    }
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        // eliminate test-scoped dependencies (no need in maven central POMs)
        root.dependencies.removeAll { dep ->
            dep.scope == "test"
        }

        // add all items necessary for maven central publication
        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST

            description 'A simple framework to enable non-blocking on spring rest controllers and services'
            name 'Spring Non-Blocking'
            url 'https://github.com/dondragon2/spring-nonblocking'
            issueManagement {
                system 'GitHub'
                url 'https://github.com/dondragon2/spring-nonblocking/issues'
            }
            licenses {
                license {
                    name 'Apache License 2.0'
                    url 'https://github.com/dondragon2/spring-nonblocking/master/LICENSE'
                    distribution 'repo'
                }
            }
            scm {
                url 'https://github.com/dondragon2/spring-nonblocking'
                connection 'scm:git:git://github.com/dondragon2/spring-nonblocking.git'
                developerConnection 'scm:git:ssh://git@github.com:dondragon2/spring-nonblocking.git'
            }
            developers {
                developer {
                    name 'Donald Walters'
                }
            }
        }
    }
}


model {
    tasks.generatePomFileForMavenJavaPublication {
        destination = file("$buildDir/generated-pom.xml")
    }
    tasks.publishMavenJavaPublicationToMavenLocal {
        dependsOn project.tasks.signArchives
    }
    tasks.publishMavenJavaPublicationToMavenRepository {
        dependsOn project.tasks.signArchives
    }
}
